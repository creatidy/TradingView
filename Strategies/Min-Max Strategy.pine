//@version=4
strategy("Min-Max Strategy", overlay=true,
     initial_capital=1000000, 
     commission_type=strategy.commission.percent, 
     commission_value=0.075,
     currency = "USD",
     default_qty_type = strategy.percent_of_equity,
     default_qty_value = 100)


session_period = input(1, minval=1, title="1 - 24h, 2 - others")
long_term = input(130, minval=1, title="SMA long (days)") // 130 days (24 * 130 / 4) 
length_sma_long_term = timeframe.isintraday and timeframe.isminutes ? long_term * 24 / session_period * 60 / timeframe.multiplier: long_term
vlt = input(5, minval=1, title="Volatility window (days)")
vlt_window = timeframe.isintraday and timeframe.isminutes ? vlt * 24 / session_period * 60 / timeframe.multiplier : vlt
sma_long_term = sma(close, length_sma_long_term)
relative_strength = (close - sma_long_term) / sma_long_term
rs = sma_long_term + sma_long_term * sma(relative_strength, length_sma_long_term)

close_upper_channel = highest(high, vlt_window)
close_lower_channel = lowest(low, vlt_window)

// === INPUT BACKTEST RANGE ===
FromYear  = input(defval = 2016, title = "From Year", minval = 2009)
FromMonth = input(defval = 6, title = "From Month", minval = 1, maxval = 12)
FromDay   = input(defval = 1, title = "From Day", minval = 1, maxval = 31)
ToYear    = input(defval = 9999, title = "To Year", minval = 2017)
ToMonth   = input(defval = 1, title = "To Month", minval = 1, maxval = 12)
ToDay     = input(defval = 1, title = "To Day", minval = 1, maxval = 31)

// === FUNCTION EXAMPLE ===
start     = timestamp(FromYear, FromMonth, FromDay, 00, 00)  // backtest start window
finish    = timestamp(ToYear, ToMonth, ToDay, 23, 59)        // backtest finish window
window()  => time >= start and time <= finish ? true : false // create function "within window of time"

float long_stop_limit = na
float short_stop_limit = na

if not na(rs[1])
    if rs < close_lower_channel and rs > rs[1]
        long_stop_limit := rs
    else
        long_stop_limit := close_lower_channel
    if rs > close_upper_channel and rs < rs[1]
        short_stop_limit := rs 
    else
        short_stop_limit := close_upper_channel

// Exit
if strategy.position_size > 0
    long_stop_limit := long_stop_limit > long_stop_limit[1] ? long_stop_limit : long_stop_limit[1]
    strategy.exit(id="XL STP", stop=long_stop_limit)
    short_stop_limit := na

if strategy.position_size < 0
    short_stop_limit := short_stop_limit < short_stop_limit[1] ? short_stop_limit : short_stop_limit[1]
    strategy.exit(id="XS STP", stop=short_stop_limit)
    long_stop_limit := na

if strategy.position_size == 0
    strategy.cancel_all()
    strategy.order(id="EL-UC", long=true, when=close_lower_channel > rs and window(), stop=close_upper_channel)
    strategy.order(id="ES-UC", long=false, when=close_upper_channel < rs and window(), stop=close_lower_channel)

plot(long_stop_limit, color=color.green, style=plot.style_cross, linewidth=2)
plot(short_stop_limit, color=color.red, style=plot.style_cross, linewidth=2)

plot(sma_long_term, color=color.yellow)
plot(rs, color=color.gray)

plot(close_upper_channel, color=color.green)
plot(close_lower_channel, color=color.red)
